import java.text.SimpleDateFormat

plugins {
    id 'org.jetbrains.kotlin.jvm' version "${kotlinVersion}" apply false
    id 'maven-publish'
}

def revision = {
    if (System.getenv("AUTOMATION_REVISION")) {
        return System.getenv("AUTOMATION_REVISION")
    }
    try {
        return "git rev-parse HEAD".execute().text.trim()
    } catch (Exception error) {
        logger.warn("git is unavailable in build environment", error)
        "unknown"
    }
}()

allprojects {
    apply plugin: 'kotlin'
    apply plugin: 'kotlin-kapt'

    group = 'org.openapigenerator.example'
    version = '1.0.0'

    repositories {
        // Use Maven Central for resolving dependencies.
        mavenLocal()
        mavenCentral()
    }

    jar {
        manifest {
            attributes 'Implementation-Title': archivesBaseName
            attributes 'Implementation-Version': archiveVersion
            attributes 'Revision': revision
            attributes 'Implementation-Build-Date': new SimpleDateFormat("yyyy-MM-dd hh:mm:ss").format(new Date())
        }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
        implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    }
}

def kindCreateCommand =[
    "sh",
    "-c",
    "kind get clusters | grep -q ${kubernetesKindClusterName} || kind create cluster --name=${kubernetesKindClusterName}" +
        " --image=kindest/node:v${kubernetesKindClusterVersion}"
]

task createKindCluster {
    doLast {
        exec {
            commandLine kindCreateCommand
        }
    }
}

task deleteKindCluster {
    doLast {
        exec {
            commandLine "kind", "delete", "clusters", "${kubernetesKindClusterName}"
        }
    }
}

clean.dependsOn deleteKindCluster
